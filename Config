    let ExtraDepTy = < git : { commit : Text, git : Text } | hackage : Text >

in  let ExtraDep = constructors ExtraDepTy

in  let opts =
          { haskellPkgs   = [ "lab1010-1-hs"
                            , "workdown"
                            , "turkish"
                            , "papers-on-ipfs"
                            , "post-photo-to-tumblr"
                            , "f-var"
                            , "get-new-posts-on-subreddit"
                            , "ipfs-s3-bridge"
                            ]
          , rustPkgs      = [ "lab1010-1-rs", "mutbst-rs", "mutlist-rs" ]
          , stackResolver = "lts-10.3"
          , buildDeps     = [ "dhall-text", "dhall", "cargo", "stack" ]
          }

in  let map = /ipfs/QmYmByX6W9srN4cwFCEL58KzAH937Es5qyhFnWHoWnYtVH/Prelude/List/map

in  let concatSep = /ipfs/QmYmByX6W9srN4cwFCEL58KzAH937Es5qyhFnWHoWnYtVH/Prelude/Text/concatSep

in  let mapText = map Text Text

in  let pkg = λ(nm : Text) → "pkg/" ++ nm

in  { cargo =
        { dependencies = { lab1010-1-rs = { path = "pkg/lab1010-1-rs" }
                         , mutlist-rs   = { path = "pkg/mutlist-rs"   }
                         , mutbst-rs    = { path = "pkg/mutbst-rs"    } }
        , workspace    = { members = mapText pkg opts.rustPkgs }
        }
    , stack =
        { extra-deps =
            [ ExtraDep.hackage "attoparsec-conduit-1.1.0"
            , ExtraDep.hackage "reddit-0.2.2.2"
            , ExtraDep.hackage "api-builder-0.14.0.0"
            , ExtraDep.hackage "unliftio-0.2.4.0"
            , ExtraDep.git
              { commit = "3c6aa9a4db2f4983e914f0e8d6fcea3ba3776a6b"
              , git    = "git@github.com:commercialhaskell/rio.git"
              }
            ]
        , packages   = mapText pkg opts.haskellPkgs
        , resolver   = opts.stackResolver
        }
    , nix = ''
        with import <nixpkgs> {};
        stdenv.mkDerivation {
          name = "toys";
          buildInputs = [ ${concatSep " " opts.buildDeps} ];
        }
      ''
    , cargo-lock = ''
        [root]
        name = "lab1010-1-rs"
        version = "0.1.0"
        dependencies = [
         "text_io 0.1.6 (registry+https://github.com/rust-lang/crates.io-index)",
        ]

        [[package]]
        name = "text_io"
        version = "0.1.6"
        source = "registry+https://github.com/rust-lang/crates.io-index"

        [metadata]
        "checksum text_io 0.1.6 (registry+https://github.com/rust-lang/crates.io-index)" = "090d2ee73ae8f01646d500fbb72f99f0b2aea174b7b15d58359bbdf083f73e6d"
      ''
    }
